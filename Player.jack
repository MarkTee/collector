/** Collector - by Mark Thomas
 *  https://github.com/marktee
 *
 *  Implements the player-controlled character. 
 */
class Player {
    field int x, y; // character's position
    field int size; // modifies character's size: actual size = (size * 2) + 1

    /** Constructs a new Player character. */
    constructor Player new() {
        // begin the game in the center of the play area
        let x = 250;
        let y = 128;
        let size = 5;
        return this;
    }

    /** Frees memory and destroys the object. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
    
    /** Draws the character. */
    method void draw(boolean color) {
        do Screen.setColor(color);
        do Screen.drawRectangle(x - size, y - size,
                                x + size, y + size);
        return;
    }
    
    /** Moves the player in the specified direction: 
     *  1 - up
     *  2 - left
     *  3 - down
     *  4 - right
     *  Movement occurs on a grid, and stays within the border. 
     */
    method void move(int direction) {
        // erase the player's old position
        do draw(false);

        // horizontal movement
        if (direction = 2) { if (x > 8) { let x = x - 11; }}   // left
        if (direction = 4) { if (x < 503) { let x = x + 11; }} // right
    
        // vertical movement
        if (direction = 1) { if (y > 18) { let y = y - 11; }}  // up
        if (direction = 3) { if (y < 249) { let y = y + 11; }} // down
        
        // draw the player's new position
        do draw(true);

        return;
    }

    /** Return the player's current coordinates. */
    method int getX() { return x; }
    method int getY() { return y; } 

}
