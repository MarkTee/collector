/** Collector - by Mark Thomas
 *  https://github.com/marktee
 *
 *  Implements the enemies that the player must avoid. 
 */
class Enemy {
    field int x, y; // enemy's position
    field int size; // size of the enemy (by default, same size as the player)
    field int direction; // direction of enemy's movement
    
    /** Constructs a new Enemy. */
    constructor Enemy new(int playerX, int playerY) {
        let size = 5;
        do spawn(playerX, playerY);
        return this;
    }
    
    /** Frees memory and destroys the object. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
    
    /** Spawns the enemy to a random location on the screen. 
     *  Never spawns the enemy in the same column or row that the player is
     *  currently occupying.
     */
    method void spawn(int playerX, int playerY) {
        let x = playerX;
        let y = playerY;
        
        // ensure the enemy spawns in a unique column and row
        while (x = playerX) {
            let x = 8 + (11 *  Random.randRange(45));
        }
        while (y = playerY) {
            let y = 18 + (11 * Random.randRange(21));
        }

        // generate a random movement direction
        let direction = Random.randRange(3) + 1;

        do draw(true);
        return;
    }

    /** Draws the enemy. */
    method void draw(boolean color) {
        // if the item is being erased, don't do anything else
        if (~(color)) {
            do Screen.setColor(false); 
            do Screen.drawCircle(x, y, 5);
            return;
        }

        // draw the enemy
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, 5);
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, 4);
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, 3);

        return;
    }

    /** Moves the enemy in the specified direction: 
     *  1 - up
     *  2 - left
     *  3 - down
     *  4 - right
     *  Movement occurs on a grid, and stays within the border. 
     */
    method void move() {
        // erase the enemy's old position
        do draw(false);

        // horizontal movement
        if (direction = 2) {
            if (x = 8) { let direction = 4; }
            if (x > 8) { let x = x - 11; }
        }
        if (direction = 4) {
            if (x = 503) { let direction = 2; let x = x - 11; }
            if (x < 503) { let x = x + 11; }
        }
    
        // vertical movement
        if (direction = 1) {
            if (y = 18) { let direction = 3; }
            if (y > 18) { let y = y - 11; }
        }
        if (direction = 3) {
            if (y = 249) { let direction = 1; let y = y - 11; }
            if (y < 249) { let y = y + 11; }
        }
        
        // draw the enemy's new position
        do draw(true);

        return;
    }
}
