/** Implements the item that the player picks up to score points. */
class Item {
    field int x, y; // item's position
    field int size; // size of the item (by default, same size as the player)

    /** Constructs a new Item. */
    constructor Item new() {
        let size = 5;
        return this;
    }

    /** Frees memory and destroys the object. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Places the item at a new position on the screen. */
    method void newPosition() {
        let x = 8 + (11 *  Random.randRange(45));
        let y = 18 + (11 * Random.randRange(21));
        do draw();
        return;
    }

    /** Draws the item. */
    method void draw() {
        // draw the item
        do Screen.setColor(true);
        do Screen.drawRectangle(x - size, y - size,
                                x + size, y + size);
        // hollow out the center of the item
        do Screen.setColor(false);
        do Screen.drawRectangle(x - size + 2, y - size + 2,
                                x + size - 2, y + size - 2);

        return;
    }

    /** Returns the item's current x-coordinate. */
    method int getX() {
        return x;
    }

    /** Returns the item's current y-coordinate. */
    method int getY() {
        return y;
    }
}
